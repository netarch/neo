pkgname=pcapplusplus
pkgver=22.11
pkgrel=1
pkgdesc="Multi-platform C++ library for capturing, parsing and crafting of network packets"
arch=("x86_64")
url="https://pcapplusplus.github.io/"
license=('Unlicense')
depends=('libpcap')
source=("https://github.com/seladb/PcapPlusPlus/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('3172f12f2f8a8902ae1ad6be5d65c3059c42c49c1a28e97e5d8d25a48b05e44f')

build() {
    cd "$srcdir/PcapPlusPlus-$pkgver"
	./configure-linux.sh \
        --default \
        --use-immediate-mode \
        --set-direction-enabled \
        --install-dir "/usr"
	make libs
}

package() {
    cd "$srcdir/PcapPlusPlus-$pkgver"
    sed -i -e "s,^INSTALL_DIR=.*$,INSTALL_DIR=$pkgdir/usr," mk/install.sh
    make install
    rm -rf "$pkgdir/usr/etc"
    sed -i -e "s,$pkgdir/usr,/usr," "$pkgdir/usr/lib/pkgconfig/PcapPlusPlus.pc"
}
