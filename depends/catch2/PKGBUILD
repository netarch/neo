pkgname=catch2
pkgver=3.0.1
pkgrel=1
pkgdesc="Modern, C++-native, test framework for unit-tests, TDD and BDD"
arch=('any')
url="https://github.com/catchorg/Catch2"
license=('Boost')
makedepends=('cmake' 'python') # python seems to be necessary for building tests (FS#60273)
source=("https://github.com/catchorg/Catch2/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('8c4173c68ae7da1b5b505194a0c2d6f1b2aef4ec1e3e7463bde451f26bbaf4e7')

build() {
    cd "$srcdir/Catch2-$pkgver"
    export CFLAGS+=" ${CPPFLAGS}"
    export CXXFLAGS+=" ${CPPFLAGS}"
    cmake -B build \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCATCH_BUILD_TESTING=ON \
        -DCATCH_BUILD_EXAMPLES=OFF \
        -DCATCH_ENABLE_COVERAGE=OFF \
        -DCATCH_ENABLE_WERROR=OFF
    make -C build
}

package() {
    cd "$srcdir/Catch2-$pkgver"
    make -C build DESTDIR="$pkgdir" install
}
